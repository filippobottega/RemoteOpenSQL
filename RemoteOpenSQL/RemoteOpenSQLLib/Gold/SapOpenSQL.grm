! -----------------------------------------------------------------------------------
! RemoteOpenSQL
!
! © 2011 Filippo Bottega
! email: filippo.bottega@gmail.com
! www.remoteopensql.com
!
! -----------------------------------------------------------------------------------

"Name"         = 'RemoteOpenSQL'
"Version"      = '2011'
"About"        = 'RemoteOpenSQL is a SAP OpenSQL derived grammar. Some rules derives from SQL89 grammar.'

"Start Symbol" = <Query>

! =============================================================================
! Comments
! =============================================================================

Comment Start = '/*'
Comment End   = '*/'
Comment Line  = '--'

! =============================================================================
! Sets
! =============================================================================

{Quote}                    = ['']
{PrintableMinusQuote}      = {Printable} - {Quote} 
{AlphanumericAndUndescore} = {Alphanumeric} + [_] 
{LetterAndUndescore}       = {Letter} + [_] 

! =============================================================================
! Terminals
! =============================================================================



StringLiteral   = ''{PrintableMinusQuote}*''
IntegerLiteral  = {Number}+
RealLiteral     = {Number}+'.'{Number}+

!----- Identifiers in SQL are very complex. 

Id                   = {Letter}{AlphanumericAndUndescore}*
ExtendedId           = _{AlphanumericAndUndescore}*
NamespaceId          = '/'{Alphanumeric}+'/'{AlphanumericAndUndescore}+
ColumnSelectionId    = (({Letter}{AlphanumericAndUndescore}*) | ('/'{Alphanumeric}+'/'{AlphanumericAndUndescore}+))'~'(({Letter}{AlphanumericAndUndescore}*) | ('/'{Alphanumeric}+'/'{AlphanumericAndUndescore}+))
ComponentSelectionId = (({LetterAndUndescore}{AlphanumericAndUndescore}*) | ('/'{Alphanumeric}+'/'{AlphanumericAndUndescore}+))'-'(({LetterAndUndescore}{AlphanumericAndUndescore}*) | ('/'{Alphanumeric}+'/'{AlphanumericAndUndescore}+))

! =============================================================================
! Rules
! =============================================================================

<Query>       ::= <Select Stm>

! =============================================================================
! Select Statement
! =============================================================================

<Select Stm>    ::= SELECT <Columns> <From Clause> <Into Clause> <Where Clause> <Order Clause>

<Columns>       ::= ' *'
                  | <Column List>

<Column List>   ::= <Column Item> <Column List>
                  | <Column Item>

<Column Item>   ::= <Column Name>
                  | <Column Name> AS <Column Alias>

<Column Name>   ::= Id
                  | NamespaceId
                  | ColumnSelectionId

<Column Alias>  ::= Id
                  
<From Clause>   ::= FROM <Source> <Client Specified Clause> <Up To Clause> 

<Source>        ::= <Table Item>
                  | <Enclosed Table Item>
                  | <Join Expression>
                  | <Enclosed Join>

<Table Item>   ::= <Table Name>
                  | <Table Name> AS <Table Alias>

<Table Name>   ::= Id
                  | NamespaceId

<Table Alias>  ::= Id

<Enclosed Table Item> ::= '(' <Table Item> ')' 

<Join Expression> ::= <Table Item> <Join Clause> <Table Item> ON <Join Condition>
                    | <Enclosed Join> <Join Clause> <Table Item> ON <Join Condition>

<Enclosed Join>     ::= '(' <Join Expression> ')'

<Join Clause>     ::= JOIN
                    | INNER JOIN
                    | LEFT JOIN
                    | LEFT OUTER JOIN

<Join Condition>  ::= <Join Comparison> AND <Join Condition>
                    | <Join Comparison>

<Join Comparison> ::= ColumnSelectionId <Comparison Operator> ColumnSelectionId

<Comparison Operator> ::= ' ='
                        | EQ
                        | ' <>'
                        | NE
                        | ' <'
                        | LT
                        | ' >'
                        | GT
                        | ' <='
                        | LE
                        | ' >='
                        | GE

<Client Specified Clause> ::= 'CLIENT SPECIFIED'
                  | 

<Up To Clause>  ::= 'UP TO uptorows ROWS'
                  | 

<Into Clause>   ::= 'INTO CORRESPONDING FIELDS OF TABLE ta_lines'

<Where Clause>  ::= WHERE <Expression>
                  |

<Order Clause>  ::= 'ORDER BY' <Order List>
                  | 'ORDER BY PRIMARY KEY'

<Order List>    ::= <Order Name> <Order List>
                  | <Order Name>

<Order Name>    ::= Id
                  | NamespaceId
                  | ColumnSelectionId

! ============================================================================= 
! Expressions
! =============================================================================

<Expression>  ::= <And Exp> OR <Expression>
                | <And Exp>

<And Exp>     ::= <Not Exp> AND <And Exp>
                | <Not Exp>

<Not Exp>     ::= NOT <Pred Exp>
                | <Pred Exp>

<Pred Exp>    ::= <Col> BETWEEN <Dobj> AND <Dobj>
                | <Col> 'NOT BETWEEN' <Dobj> AND <Dobj>
                | <Col> 'IS NOT NULL'
                | <Col> 'IS NULL'
                | <Col> LIKE StringLiteral
                | <Col> <Comparison Operator> <Dobj>
                | <Col>
                | '(' <Expression> ')'

<Col>         ::= Id
                | NamespaceId
                | ColumnSelectionId

<Dobj>        ::= IntegerLiteral
                | RealLiteral
                | StringLiteral
                | NULL
                | Id
                | ExtendedId
                | NamespaceId
                | ColumnSelectionId
                | ComponentSelectionId


